import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
			jni.srcDirs = []
			jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 17
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_5
            targetCompatibility JavaVersion.VERSION_1_5
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':j2se')
    compile files('libs/libprotobuf.jar')
}

// call regular ndk-build(.cmd) script from app directory
task ndkBuild(type: Exec) {
	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		commandLine 'ndk-build.cmd', '-C', file('.').absolutePath
	} else {
		commandLine 'ndk-build', '-C', file('.').absolutePath
	}
}

tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn ndkBuild
}
